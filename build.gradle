apply plugin: 'java'

group 'com.github.RainAng'
version '0.1.0'
def libVersion = '0.6.2'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	maven { url "https://jitpack.io" }
}

configurations {
	shade
	compile.extendsFrom shade
}

dependencies {
	shade "com.github.RainAng:MinesweeperLib:v$libVersion"
}

jar {
	manifest {
		attributes "Class-Path": "MinesweeperLib-${libVersion}.jar",
				"Main-Class": 'com.github.rainang.minesweeperfx.MinesweeperFX'
	}
	configurations.shade.each { dep ->
		from(project.zipTree(dep)) {
			exclude 'META-INF', 'META-INF/**'
		}
	}
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
}